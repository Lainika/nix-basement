[[ch-usage]]
== Using nix-basement

nix-basement is meant to be used as an input to a nix-flake.

Example flake:

[source,nix]
----
{
  description = "A very basic flake";

  inputs = {
    base.url = "github:nix-basement/nix-basement/main";
  };

  outputs = directInputs:
    let
      inputs = directInputs.base.inputs // directInputs;
      inherit (inputs)
        base
        flake-utils
        nixpkgs
        darwin
        self
        ;
      lib = base.lib;
    in
    with builtins; with lib; (
      base.outputs
      //
      {

        nixosConfigurations = generateNixosConfigurations inputs;
        darwinConfigurations = generateDarwinConfigurations inputs;

        deploy = generateDeployConfig self;
        devShells = base.devShells;

      }
      //
      (flake-utils.lib.eachDefaultSystem (system: {

        buildJobs = generateBuildJobs self system;

      }))
    );
}
----

[[sec-usage-nixos]]
=== NixOS Configuration

nix-basement (and it's `generateNixosConfiguration` function) searches for a `hosts/{hostname}/default.nix` looking like this:

[source,nix]
----
{ ... }: {
  system = "x86_64-linux"; # or "aarch64-linux", ...
  modules = [
    # NixOS modules of this configuration, for example `[ ./configuration.nix ]` with your system specific configuraition
  ];

  # and optionally deployment configuration (deploy.rs)
  deployment = {
    targetUser = "root"; # root or a user that can sudo passwordless
    targetHost = "192.168.1.1"; # ip or hostname of the server
    substituteOnDestination = true; # if true the server tries to download needed store paths from caches
  };

}
----

[NOTE]
the `deployment` block is entirely optional and can be omitted

[[sec-usage-nix-darwin]]
=== nix-darwin Configuration

Similar to NixOS, nix-basement (and it's `generateDarwinConfiguration` function) searches for a `hosts/{hostname}/default.nix` looking like this:

[source,nix]
----
{ ... }: {
  system = "x86_64-darwin"; # or "aarch64-darwin"
  modules = [
    # nix-darwin modules of this configuration, for example `[ ./configuration.nix ]` with your system specific configuraition
  ];

}
----
